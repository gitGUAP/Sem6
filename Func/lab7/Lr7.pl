getSolution(S):-
    S=[1/_,2/_,3/_,4/_,5/_,6/_,7/_,8/_],
    solution(S).

solution([]).

% доска NxN является решением, если является решением её под-доска (N-1)*(N-1), 
% а первый ферзь не бьет ферзей на этой под-доске.
solution([X/Y | Others]) :-
    solution(Others),
    member(Y, [1, 2, 3, 4, 5, 6, 7, 8]),
    nokill(X/Y, Others).
         
% очевидно что ферзь с любыми координатами не бьет ферзей из пустого массива, 
% поскольку просто некого бить
nokill(_, []).        

% ферзь не бьет набор ферзей если он не бьет первого ферзя из набора 
% и не бьет остальных ферзей набора         
nokill(X/Y, [X1/Y1 | Others]) :-
    Y =\= Y1,   % на разных горизонталях   
    Y1-Y =\= X1-X, % на разных диагоналях
    Y1-Y =\= X-X1,
    nokill(X/Y, Others).